use std::collections::HashMap;

use polytype::{Type, TypeSchema, Variable};

use crate::parser::ast::*;
use crate::parser::lexer::Tok;
use crate::error::LexicalError;

// NOTE: The type_builder is a temporary HashMap that keeps
// track of type variables and their corresponding variable number
// in polytype. For example `forall a. a -> a` creates an entry
// in the back of type_builder with { "a" : 0 }, this is clear()'d
// once the non-terminal is fully parsed.
grammar<'input>(input: &'input str, type_builder: &mut HashMap<String, usize>);

pub AST: AST = {
    <items: Item*> => AST { items }
};

Attr: Attr = {
    "@" "[" <name: Name> "(" <args: SepList<Name, ",">> ")" "]" "newline"
        => Attr { name, args }
};

Item: Node<Item> = {
    AttrNode<Definition>,
    // DataType,
    // Module,
};

Definition: Item = {
    "let" <name: Name> <ann: NL<Ann>?> NL<"="> <expr: Expr> "newline"
        => Item::Definition { name, ann, expr }
};

// DataType: ItemKind = {
//     "data" <schema: TypeSchema> "newline"
//         <variants: SepList<Variant, NL<",">>>
//     NL<"end"> => {
//         type_builder.clear();
//         ItemKind::DataType { schema, variants }
//     }
// };
// 
// Variant: (String, Vec<(String, TypeSchema)>) = {
//     <TypeName> NL<"{">
//         <SepList<(<Name> ":" <TypeSchema>), NL<",">>>
//     "}"
// };
// 
// Module: ItemKind = {
//     "mod" <name: Name> "newline"
//         <items: Item*> 
//     "end" => ItemKind::Module { name, items }
// };

Ann: TypeSchema = {
    ":" <TypeSchema> => {
        // The type_build should always be empty before we
        // start parsing a type annotation; it's empty when
        // first created so this is fully guaranteed.
        type_builder.clear();
        <>
    }
};

SimpleMonoType: Type = {
    "(" <MonoType> ")",
    // NOTE: the convention is that type-variables are lower-case,
    // but constructor names start with an upper-case.
    <v: Name> => Type::Variable(
        // FIXME: Figure out how to do proper error reporting
        // in the parser! (probably by passing in a mut data-type).
        *type_builder.get(&v).unwrap()
    )
}

MonoType: Type = {
    SimpleMonoType,
    <n: TypeName> <ps: SimpleMonoType*> => Type::Constructed(
        // TODO: is it better to lear the String or to switch N to String,
        // alltogether? At least in the second case the names will be freed
        // once the we're done with them i.e the typechecker!
        // There is a small hurdle as polytype only implements `Name` for
        // &'static str.
        Box::leak(n.into_boxed_str()),
        ps
    ),
}

// NOTE: this part had to be seperated from the TypeSchema terminal,
// since the .insert(..) wasn't getting executed before attempting
// to parse the reset of the annotation, and so the variables weren't
// present.
TypeQuantifier: Variable = {
    // FIXME: we shouldn't force the poor programmers to type `forall`
    // everytime they need to do polymorphism, oh the horror!
    "forall" <v: Name> "." => {
        // The number of the `v` is picked as the maximum existing
        // type variable PLUS one. Otherwise we put in zero.
        let variable = match type_builder.values().max() {
            None => 0,
            Some(v) => v + 1,
        };
        type_builder.insert(v, variable);
        variable
    }
}

TypeSchema: TypeSchema = {
    SepList1<MonoType, "->"> => TypeSchema::Monotype(Type::from(<>)),
    <v: TypeQuantifier> <t: TypeSchema> =>
        TypeSchema::Polytype {
            variable: v,
            body: Box::new(t),
        }
};

Stmt: Stmt = {
    Item             => Stmt::Item(<>),
    <Expr> "newline" => Stmt::Expr(<>),
};

Expr: Node<Expr> = {
    Infix,
    // TODO: See if we can remove the necessary () around |x| do .. end
    // functions by having two seperate terminals, one for "single-line"
    // lambdas and another do "multi-line" lambdas.
    Node<Lambda>,
    // Assign,
};

Lambda: Expr = {
    NL<"fn"> <params: NL<Name>+> NL<"=>"> <e: Expr> => Expr::Lambda { params, expr: Box::new(e) }
};

Infix: Node<Expr> = {
    NApply,
    Node<Apply>,
//     <l: Infix> <op: Operator> <r: NInfix> => Expr::Apply {
//         left: Box::new(Expr::Apply { 
//             left: Box::new(Expr::Name(op)),
//             right: Box::new(l)
//         }),
//         right: Box::new(r)
//     },
};

NApply: Node<Expr> = {
    // Ellipsis,
    Node<Void>,
    Node<Lit>,
    // Bool,
    // Char,
    // List,
    // Str,
    Node<Ident>,
    // Branch,
    // Field,
    Node<DoBlock>,
    "(" <Expr> ")",
};

// Ellipsis: Expr = {
//     "..." => Expr::Ellipsis
// };

Void: Expr = {
    "(" ")" => Expr::Void
};

Lit: Expr = {
    "Int" => match <> {
        Tok::IntLiteral(i) => Expr::Lit(Lit::Int(i)),
        _ => unreachable!()
    }
};

// Bool: Expr = {
//     "true" => Expr::Bool(true),
//     "false" => Expr::Bool(false)
// };
// 
// Char: Expr = {
//     "Char" => match <> {
//         Tok::CharLiteral(c)  => Expr::Char(c),
//         _ => unreachable!()
//     }
// };
// 
// Str: Expr = {
//     "Str" => Expr::List(<>.chars()
//                           .map(|c| Expr::Char(c))
//                           .collect())
// };

// List: Expr = {
//     NL<"["> <v: SepList<Expr, NL<",">>> "]" => Expr::List(v)
// 
// }

Ident: Expr = {
    Name => Expr::Name(<>)
};

// Branch: Expr = {
//     "if" <e: Expr> NL<"then"> <b1: Block>
//     <b2: (NL<"elif"> <Expr> NL<"then"> <Block>)*>
//     <b3: (NL<"else"> <Block>)?> "end"
//     => { 
//         // FIXME: the parser should not arbitrarily insert AST nodes,
//         // this should ideally be moved much further down.
//         let b3 = match b3 {
//             // If the user doesn't provide an else clause
//             // we insert one with a `()` expression which
//             // forces the entire expression to have type Void.
//             None    => vec![Stmt::Expr(Expr::Void)],
//             Some(v) => v
//         };
//         let mut paths = vec![(e, b1)];
//         paths.extend(b2.into_iter());
//         paths.push((
//             Expr::Bool(true),
//             b3
//         ));
//         Expr::Branch { paths }
//     }
// };

Block: Vec<Stmt> = {
    Expr => vec![Stmt::Expr(<>)],
    <Stmt+>
};

DoBlock: Expr = {
    NL<"do"> <Block> "end" => Expr::Block { body: <> },
};

Apply: Expr = {
    // It is completely unecessary to go the Haskell route and make this rule
    // recursive with respect to Apply. While this is not an accurate representation
    // of applications in Lambda Calclus, it is a flat form that's easier to handle.
    <left: NApply> <right: NApply+> => Expr::Apply { left: Box::new(left), right },
};

// Field: Expr = {
//     <expr: NApply> "." <name: Name>
//         => Expr::Field { expr: Box::new(expr), name }
// };
// 
// Assign: Expr = {
//     <left: Field> "=" <right: Expr>
//         => Expr::Assign { left: Box::new(left), right: Box::new(right) }
// };
 
TypeName: String = <"TypeName"> => <>.to_string();
Name:     String = {
    <"Name"> => <>.to_string(),
    "(" <Operator> ")"
};

Operator:       String = <"Operator">   => format!("({})", <>).to_string();

NL<T>: T     = <T> "newline"?;

SepList<T, S>: Vec<T> = {
    <mut v: (<T> S)*> <e: NL<T>?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

SepList1<T, S>: Vec<T> = {
    <mut v: (<T> S)*> <e: T> => { v.push(e); v }
};

Many2<T>: Vec<T> = {
    <mut v: T+> <e: T> => { v.push(e); v }
};

Node<T>: Node<T> = {
    <start: @L> <inner: T> <end: @R> => Node {
        span: Span { start, end },
        attr: None,
        inner
    }
};

AttrNode<T>: Node<T> = {
    <start: @L> <attr: Attr?> <inner: T> <end: @R> => Node {
        span: Span { start, end },
        attr,
        inner
    }
};

extern {
    type Location = usize;
    type Error = LexicalError;
    
    enum Tok<'input> {
        "Name"     => Tok::Name(<&'input str>),
        "TypeName" => Tok::TypeName(<&'input str>),
        "Operator" => Tok::Operator(<&'input str>),

        "Int"      => Tok::IntLiteral(i64),
        "Str"      => Tok::StrLiteral(<&'input str>),
        "Char"     => Tok::CharLiteral(char),

        "..."      => Tok::Ellipsis,

        "@"        => Tok::At,
        "."        => Tok::Dot,

        "mod"      => Tok::Mod,
        "let"      => Tok::Let,
        "fn"       => Tok::Fn,
        "do"       => Tok::Do,
        "end"      => Tok::End,
        "data"     => Tok::Data,
        "forall"   => Tok::Forall,

        "true"     => Tok::True,
        "false"    => Tok::False,
        "if"       => Tok::If,
        "then"     => Tok::Then,
        "elif"     => Tok::Elif,
        "else"     => Tok::Else,

        "loop"     => Tok::Loop,
        "break"    => Tok::Break,

        "..."      => Tok::Ellipsis,
        ":"        => Tok::Colon,
        "->"       => Tok::Arrow,
        "=>"       => Tok::DArrow,
        "|"        => Tok::Pipe,
        "="        => Tok::Equal,
        "~"        => Tok::Tilde,
        "@"        => Tok::At,
        "."        => Tok::Dot,
        ","        => Tok::Comma,
        "#"        => Tok::Hash,

        "("        => Tok::LParen,
        ")"        => Tok::RParen,
        "{"        => Tok::LBrace,
        "}"        => Tok::RBrace,
        "["        => Tok::LBrack,
        "]"        => Tok::RBrack,

        "newline"  => Tok::Newline
    }
}
